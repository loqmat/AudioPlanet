<!doctype html>
<html>
<head>
    <title> Computer Graphics Project </title>
    
    <script id="box-vshader" type="x-shader/x-vertex">
        uniform mat4 uProjection; 
        uniform mat4 uModelView;
        attribute vec2 vPosition;

        void
        main()
        {
            gl_Position = uProjection * uModelView * vec4(vPosition,0,1);
        }
    </script>
    <script id="box-fshader" type="x-shader/x-fragment">
        precision mediump float;
        uniform vec4 uColor;

        void
        main()
        {
            gl_FragColor = uColor;
        }
    </script>    
    
    <script id="wave-vshader" type="x-shader/x-vertex">
        attribute vec2 vPosition;

        void
        main()
        {
            gl_Position = vec4(vPosition,0,1);
            gl_PointSize = 4.0;
        }
    </script>
    <script id="wave-fshader" type="x-shader/x-fragment">
        precision mediump float;
        uniform vec4 uColor;

        void
        main()
        {
            gl_FragColor = uColor;
        }
    </script>
    
    <script id="pp-vshader" type="x-shader/x-vertex">
        attribute vec2 vPosition;
        
        varying vec2 fUV;

        void main() {
            gl_Position = vec4(vPosition * 2.0 - 1.0,0,1);
            fUV = vPosition;
        }
    </script>
    <script id="ppblurx-fshader" type="x-shader/x-fragment">
        precision mediump float;
        
        uniform sampler2D uImage;
        uniform vec2 uImageSize;
        uniform vec2 uDirection;
        uniform float uKernel[9];
        uniform vec4 uColor;
        
        varying vec2 fUV;

        void main() {
            vec4 color = vec4(0,0,0,0);
            color += uKernel[0] * texture2D(uImage, fUV + vec2(-8,0) / uImageSize);
            color += uKernel[1] * texture2D(uImage, fUV + vec2(-6,0) / uImageSize);
            color += uKernel[2] * texture2D(uImage, fUV + vec2(-4,0) / uImageSize);
            color += uKernel[3] * texture2D(uImage, fUV + vec2(-2,0) / uImageSize);
            color += uKernel[4] * texture2D(uImage, fUV + vec2( 0,0) / uImageSize);
            color += uKernel[5] * texture2D(uImage, fUV + vec2( 2,0) / uImageSize);
            color += uKernel[6] * texture2D(uImage, fUV + vec2( 4,0) / uImageSize);
            color += uKernel[7] * texture2D(uImage, fUV + vec2( 6,0) / uImageSize);
            color += uKernel[8] * texture2D(uImage, fUV + vec2( 8,0) / uImageSize);
            gl_FragColor = uColor * color;
        }
    </script>
    <script id="ppblury-fshader" type="x-shader/x-fragment">
        precision mediump float;
        
        uniform sampler2D uImage;
        uniform vec2 uImageSize;
        uniform vec2 uDirection;
        uniform float uKernel[9];
        uniform vec4 uColor;
        
        varying vec2 fUV;

        void main() {
            vec4 color = vec4(0,0,0,0);
            color += uKernel[0] * texture2D(uImage, fUV + vec2(0,-8) / uImageSize);
            color += uKernel[1] * texture2D(uImage, fUV + vec2(0,-6) / uImageSize);
            color += uKernel[2] * texture2D(uImage, fUV + vec2(0,-4) / uImageSize);
            color += uKernel[3] * texture2D(uImage, fUV + vec2(0,-2) / uImageSize);
            color += uKernel[4] * texture2D(uImage, fUV + vec2(0, 0) / uImageSize);
            color += uKernel[5] * texture2D(uImage, fUV + vec2(0, 2) / uImageSize);
            color += uKernel[6] * texture2D(uImage, fUV + vec2(0, 4) / uImageSize);
            color += uKernel[7] * texture2D(uImage, fUV + vec2(0, 6) / uImageSize);
            color += uKernel[8] * texture2D(uImage, fUV + vec2(0, 8) / uImageSize);
            gl_FragColor = uColor * color;
        }
    </script>
    <script id="ppfinal-fshader" type="x-shader/x-fragment">
        precision mediump float;
        uniform sampler2D uPost;
        uniform sampler2D uOriginal;
        uniform vec4 uColor;
        uniform float uExposure;

        varying vec2 fUV;

        void main() {
            const float gamma = 2.2;
            vec4 original = texture2D( uOriginal, fUV );
            vec4 postproc = texture2D( uPost, fUV );
            
            vec3 color = original.rgb + postproc.a * postproc.a * postproc.rgb;
            
            vec3 result = pow(vec3(1.0) - exp(-color*uExposure), vec3(1.0 / gamma));
            gl_FragColor = vec4(result, 1.0);
        }
    </script>
    
    <script id="sphere-vshader" type="x-shader/x-vertex">
        // AUDIO_ELEMENTS will be defined later
        #define M_PI 3.1415926535897932384626433832795
        #define M_LOG2 0.30102999566
        
        uniform float audioData[AUDIO_ELEMENTS];
        uniform float audioImpulse[AUDIO_ELEMENTS];
        uniform vec3 audioBumps[AUDIO_ELEMENTS];
        uniform vec3 normalGradientColors[4];
        uniform vec3 impulseGradientColors[4];
        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;
        uniform float baseHeight;
        uniform float basePointSize;
        uniform float heightScale0;
        uniform float heightScale1;
        
        attribute vec2 vPosition;
        attribute vec2 vUV;
        
        varying float fCombine;
        varying vec2 fUV;
        varying vec3 fNormal;
        varying vec3 fColor;
        
        void main() {
            float theta = vPosition.x;
            float phi = vPosition.y;
            float cosTheta = cos(theta);
            float sinTheta = sin(theta);
            float cosPhi = cos(phi);
            float sinPhi = sin(phi);
            
            vec3 position = normalize(vec3(cosPhi * sinTheta, cosTheta, sinPhi * sinTheta));
            vec3 normal = position;
            
            fCombine = 0.0;
            float h = baseHeight;
            float gauss_scale = float(AUDIO_ELEMENTS)/4.0;
            
            for ( int i=0;i<AUDIO_ELEMENTS;i++ ) {
                float distance = acos(clamp(dot(position, audioBumps[i]),-1.0,1.0)) / M_PI;
                float expon = gauss_scale*distance*distance;
                float gauss = exp2(-expon);
                float deriv = distance * gauss_scale * M_LOG2 * exp2(1.0-expon);
                float difer = 1.0 / (1.0 + deriv);
                
                //normal += (1.0-difer) * audioBumps[i] + difer * position;
                
                float height = audioData[i] * gauss;
                h += (heightScale0 * height * height) / heightScale1;
                fCombine += audioImpulse[i] * gauss;
            }
            
            //normal /= float(AUDIO_ELEMENTS);
            fCombine /= 10.0;
            
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position * h, 1);
            gl_PointSize = h * h * basePointSize / (1.0+gl_Position.z);
            
            fUV = vUV;
            fNormal = normal;
            
            float A = 1.0 - clamp(abs(h-0.0), 0.0, 1.0);
            float B = 1.0 - clamp(abs(h-1.0), 0.0, 1.0);
            float C = 1.0 - clamp(abs(h-2.0), 0.0, 1.0);
            float D = 1.0 - clamp(abs(h-3.0), 0.0, 1.0);
            float E = A + B + C + D;
            
            vec3 normalColor = normalGradientColors[0] * A / E + 
                               normalGradientColors[1] * B / E + 
                               normalGradientColors[2] * C / E + 
                               normalGradientColors[3] * D / E;
            vec3 impulseColor = impulseGradientColors[0] * A / E + 
                                impulseGradientColors[1] * B / E + 
                                impulseGradientColors[2] * C / E + 
                                impulseGradientColors[3] * D / E;
                                
            fColor = fCombine * impulseColor + (1.0-fCombine) * normalColor;
        }
    </script>
    
    <script id="sphere-fshader" type="x-shader/x-fragment">
        precision mediump float;    
        varying float fCombine;
        varying vec2 fUV;
        varying vec3 fNormal;
        varying vec3 fColor;
        
        uniform vec3 uLightDirection;
        uniform vec4 uColor;

        void main() {
            float stren = clamp(dot(uLightDirection, fNormal), 0.0, 1.0);
            float glow = max(fCombine - 0.85, 0.0);
            gl_FragColor = vec4(fCombine * fColor + (1.0-fCombine) * fColor * stren, glow * glow);
        }
    </script>
    
    <script id="mirror-vshader" type="x-shader/x-vertex">
        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        
        attribute vec3 vPosition;
        
        void main() {
            gl_Position = projectionMatrix * modelViewMatrix * vec4(vPosition, 1);
        }
    </script>
    
    <script id="mirror-fshader" type="x-shader/x-fragment">
        precision mediump float;
        
        uniform vec4 uColor;
        
        void main() {
            gl_FragColor = uColor;
        }
    </script>
    
    <script type="text/javascript" src="./lib/webgl-utils.js"></script>
    <script type="text/javascript" src="./lib/initShaders.js"></script>
    <script type="text/javascript" src="./lib/MV.js"></script>
    <script type="text/javascript" src="./generateSphere.js"></script>
    <script type="text/javascript" src="./util.js"></script>
    <script type="text/javascript" src="./data.js"></script>
    <script type="text/javascript" src="./audio.js"></script>
    <script type="text/javascript" src="./setup.js"></script>
    <script type="text/javascript" src="./resize.js"></script>
    <script type="text/javascript" src="./draw.js"></script>
    <script type="text/javascript" src="./ui.js"></script>
    <script type="text/javascript" src="./main.js"></script>
    <link rel="stylesheet" type="text/css" href="main.css">
</head>
<body>
    <input id="loadFile" type="file"/>
    <canvas id="GLCanvas">
        Oops ... your browser doesn't support the HTML5 canvas element
    </canvas>
    <canvas id="TextCanvas">
    </canvas> 
</body>
</html>